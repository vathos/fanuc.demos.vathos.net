PROGRAM get_pose
VAR
  connection      : STRING[16]
  status          : INTEGER
  fid             : FILE
  response        : STRING[128]
  workflow        : STRING[32]
  pr_index        : INTEGER
  timeout         : INTEGER
  timeout_str     : STRING[8]
%INCLUDE utils.h
BEGIN
  GET_TPE_STR(1, connection)
  GET_TPE_STR(2, workflow)
  GET_TPE_INT(3, timeout)
  GET_TPE_INT(4, pr_index)
  -- cast timeout to string 
  CNV_INT_STR(timeout, 8, 0, timeout_str)
  
  -- clear and re-establish connection
  MSG_DISCO(connection, status)
  MSG_CONNECT(connection, status)
  IF status<>0 THEN
    WRITE TPERROR('[get_pose] Connection failed with status ', status, CR)
    ABORT
  ENDIF

  -- open file handle and write message
  SET_FILE_ATR(fid, ATR_IA)
  OPEN FILE fid('rw', connection)
  WRITE fid('get_pose;' + workflow + ';xyz;1;' + timeout_str, CR)
  
  status = IO_STATUS(fid)
  IF status<>0 THEN
    WRITE TPERROR('[get_pose] Could not open socket for writing with status', status, CR)
    MSG_DISCO(connection, status)
    ABORT
  ENDIF

  -- read response
  response = ''
  READ fid(response)
  status = IO_STATUS(fid)
  IF status<>0 THEN
    CLOSE FILE fid
    MSG_DISCO(connection, status)
    WRITE TPERROR('[get_pose] Read response failed with status', status, CR)
    ABORT
  ENDIF

  -- check for server-side errors
  IF SUB_STR(response, 1, 1)='-' THEN
    WRITE TPERROR('[get_pose] Server error', CR)
  ENDIF
  
  

  CLOSE FILE fid
  MSG_DISCO(connection, status)  
END get_pose