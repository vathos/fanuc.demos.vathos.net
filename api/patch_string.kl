PROGRAM patch_string
VAR
  connection      : STRING[16]
  product_id      : STRING[32]
  service         : STRING[32]
  key             : STRING[32]
  value           : STRING[32]
  status          : INTEGER
  fid             : FILE
  response        : STRING[128]
%INCLUDE utils.h
BEGIN
  GET_TPE_STR(1, connection)
  GET_TPE_STR(2, product_id)
  GET_TPE_STR(3, service)
  GET_TPE_STR(4, key)
  GET_TPE_STR(5, value)
  
  -- clear and re-establish connection
  MSG_DISCO(connection, status)
  MSG_CONNECT(connection, status)
  IF status<>0 THEN
    WRITE TPERROR('[patch_string] Connection failed with status ', status, CR)
    ABORT
  ENDIF

  -- open file handle and write message
  SET_FILE_ATR(fid, ATR_IA)
  OPEN FILE fid('rw', connection)
  -- FIXME: upgrade this to a product/service pair
  WRITE fid('patch_configuration;' + product_id + ';' + key + ';0,' + value, CR)
  
  status = IO_STATUS(fid)
  IF status<>0 THEN
    WRITE TPERROR('[patch_string] Could not open socket for writing with status', status, CR)
    MSG_DISCO(connection, status)
    ABORT
  ENDIF

  -- read response
  response = ''
  READ fid(response)
  status = IO_STATUS(fid)
  IF status<>0 THEN
    CLOSE FILE fid
    MSG_DISCO(connection, status)
    WRITE TPERROR('[patch_string] Read response failed with status', status, CR)
    ABORT
  ENDIF

  -- check for server-side errors
  IF SUB_STR(response, 1, 1)='-' THEN
    WRITE TPERROR('[patch_string] Server error', CR)
  ENDIF
  
  CLOSE FILE fid
  MSG_DISCO(connection, status)  
END patch_string