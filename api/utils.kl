PROGRAM utils
%NOLOCKGROUP
CONST
    TPE_TYP_INT  = 1
    TPE_TYP_REAL = 2
    TPE_TYP_STR  = 3

ROUTINE parse_args(param_no : INTEGER; exp_type : INTEGER; int_val : INTEGER; real_val : REAL; str_val : STRING)
VAR
    dataType  : INTEGER
    status    : INTEGER
BEGIN
    GET_TPE_PRM(param_no, dataType, int_val, real_val, str_val, status)
    IF status<>0 THEN
        WRITE TPERROR('[parse_args] could not get parameter', param_no, CR)
        ABORT
    ENDIF

    IF dataType<>exp_type THEN
        WRITE TPERROR('[parse_args] could not get parameter', param_no, CR)
        ABORT
    ENDIF
END parse_args

ROUTINE parse_int_arg(param_no : INTEGER; int_val : INTEGER)
VAR
    real_val : REAL
    str_val  : STRING[1]
BEGIN
    parse_args(param_no, TPE_TYP_INT, int_val, real_val, str_val)
END parse_int_arg

ROUTINE parse_str_arg(param_no : INTEGER; str_val : STRING)
VAR
    int_val  : INTEGER
    real_val : REAL
BEGIN
    parse_args(param_no, TPE_TYP_STR, int_val, real_val, str_val)
END parse_str_arg

ROUTINE split_str(str:STRING; sep:STRING; out:ARRAY[*] OF STRING)
  VAR
    i   : INTEGER
    k   : INTEGER
	start    : INTEGER
  BEGIN
    k = 1

    -- initialize the whole out array to ''
    FOR i = 1 TO array_len(out) DO
      out[i] = ''
    ENDFOR

	start = 1

    FOR i = 1 TO (STR_LEN(str)-STR_LEN(sep)) DO
		IF (SUB_STR(str, i, STR_LEN(sep))=sep) THEN
			out[k] = SUB_STR(str, start, (i-start))
			k = k + 1
			start = i+STR_LEN(sep)
		ENDIF
    ENDFOR

	out[k] = SUB_STR(str, start, (STR_LEN(str)-start+1))
  END split_str

BEGIN
END utils